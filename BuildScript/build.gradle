configurations {
	svnant
}
ext {
	dep_repopath = new File("${project.rootDir}/../svn_repo/").canonicalPath
	dep_destpath = 'dep_project'
	projectApp_depProjects = ["Stab"]
}
 
dependencies {
	svnant fileTree(dir: 'lib_svnant', include: '**/*.jar')
}

task svnCheckout << {
	ant.taskdef(resource: 'org/tigris/subversion/svnant/svnantlib.xml', classpath: configurations.svnant.asPath)
	mkdir(dep_destpath)

	projectApp_depProjects.each { def projectName ->
		def checkoutDest = "${dep_destpath}/${projectName}"
		mkdir(checkoutDest)
		ant.svn(javahl: 'false', svnkit: 'true', failonerror: 'false') {
			ant.checkout(url: "file://${dep_repopath}/${projectName}", destpath: checkoutDest)
			ant.update(dir: checkoutDest)
		}
	}
}

subprojects {
	apply plugin: 'java'
	
	repositories {
		mavenCentral()
	}
	
	dependencies {
	    compile 'org.slf4j:slf4j-api:1.7.5'
	    testCompile 'junit:junit:4.11'
	}
}

project(':App') {
	dependencies {
	    compile project(':dep_project/Stab')
	}

	compileJava.dependsOn svnCheckout
}
